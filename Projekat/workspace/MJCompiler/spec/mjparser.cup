package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	Logger log = Logger.getLogger(getClass());
   
    /* Following methods are redefined in purpose of different error message output */
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
    	done_parsing();
      	report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
            
        log.error(msg.toString());
    }
    
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
		
	return s;
:}


/* Keyword terminals */
terminal					PROG, BREAK, CLASS, ENUM, ELSE, CONST, IF, DO, WHILE, NEW, PRINT;
terminal					READ, RETURN, VOID, EXTENDS, CONTINUE, THIS, SUPER, GOTO, RECORD;

/* Identifier terminals */
terminal	String			IDENT;

/* Constant terminals */
terminal	Integer			NUMBER;
terminal	Character 		CHAR;
terminal	Boolean			BOOL;
	
/* Operator terminals */
terminal					PLUS, MINUS, MUL, DIV, PERCENT, EQ, NEQ, GT, GTE, LT, LTE, AND ,OR, EQUAL, PLUSPLUS;
terminal					MINUSMINUS, SEMI, COLON, COMMA, DOT, LPAREN, RPAREN, LBRACE, RBRACE, LBRACKET, RBRACKET;

/* Nonterminals */
nonterminal					DeclList, Decl;
nonterminal					ConstListDecls, ConstDeclList, ConstDecl, ConstDeclType;
nonterminal					RecordDecl;
nonterminal					VarListDeclList, VarListDecls, VarDeclList, VarDecl, VarListDeclsType;
nonterminal					MethodDeclList, MethodDecl;
nonterminal					FormPars, FormParamList, FormParam;
nonterminal					IndexingList, Indexing;
nonterminal					Statements, StatementList, Statement, SingleStatement;
nonterminal					IfStmtStart, ThenStmtStart, ElseStmtStart, DoStatementStart, WhileConditionStart;
nonterminal					DesignatorStatement;
nonterminal					OptActPars, ActPars;
nonterminal					Mulop, Addop, Assignop;

nonterminal		rs.etf.pp1.symboltable.concepts.Obj		ProgName, Program;
nonterminal		rs.etf.pp1.symboltable.concepts.Obj		MethodIdent;
nonterminal		rs.etf.pp1.symboltable.concepts.Obj		Designator, DesignatorName, FuncCallStart;
nonterminal		rs.etf.pp1.symboltable.concepts.Struct	Type, ReturnType;
nonterminal		rs.etf.pp1.symboltable.concepts.Struct	RecordName;
nonterminal		rs.etf.pp1.symboltable.concepts.Struct	Const, Expr, Term, Factor;
nonterminal		rs.etf.pp1.symboltable.concepts.Struct	Condition, CondTerm, CondFact;
nonterminal		rs.etf.pp1.symboltable.concepts.Struct	PrintPars;

/* Priorities */
precedence		left		ELSE;

/* Grammar Rules */
Program 			::=		(Program) PROG ProgName:p DeclList LBRACE MethodDeclList RBRACE;

ProgName			::=		(ProgName) IDENT:progName;

DeclList			::=		(Declarations) DeclList Decl
							|
							(NoDeclarations) /* epsilon */;
							
Decl				::=		(DeclConst) ConstListDecls
							|
							(DeclVar)	VarListDecls
							|
							(DeclRecord) RecordDecl
							|
							error SEMI
							|
							error COMMA;

ConstListDecls		::=		(ConstListDeclarations) CONST ConstDeclType ConstDeclList SEMI;

ConstDeclType		::=		(ConstDeclType) Type;

ConstDeclList		::=		(MultipleConstDecls) ConstDeclList COMMA ConstDecl
							|
							(SingleConstDecl) ConstDecl;
							
ConstDecl			::=		(ConstDecl) IDENT:constName EQUAL Const:c;

Const				::=		(NumConst) NUMBER:val
							|
							(CharConst) CHAR:val
							|
							(BoolConst) BOOL:val;		
							
Type				::=		(Type) IDENT:typeName;

VarListDecls		::=		(VarListDeclarations) VarListDeclsType VarDeclList SEMI;

VarListDeclsType	::= 	(VarListDeclsType) Type;

VarDeclList			::=		(MultipleVarDecls) VarDeclList COMMA VarDecl
							|
							(SingleVarDecl) VarDecl;
							
VarDecl				::=		(VarDeclBrackets) IDENT:varName LBRACKET RBRACKET
							|
							(VarDeclNoBrackets) IDENT:varName;

VarListDeclList		::=		(VarListDeclarationList) VarListDeclList VarListDecls
							|
							(NoVarListDeclarationList) /* epsilon */;
							
RecordDecl			::=		(RecordDeclaration) RECORD RecordName:r LBRACE VarListDeclList RBRACE;

RecordName			::=		(RecordName) IDENT:name;
							
MethodDeclList		::=		(MethodDeclarations) MethodDeclList MethodDecl
							|
							(NoMethodDeclarations) /* epsilon */;
							
MethodDecl			::=		(MethodDecl) MethodIdent:m LPAREN FormPars RPAREN VarListDeclList LBRACE StatementList RBRACE;

MethodIdent			::=		(MethodIdent) ReturnType:r IDENT:name;

ReturnType			::=		(RetType) Type:t
							|
							(RetVoid) VOID;
							
FormPars			::=		(FormParams) FormParamList
							|
							(NoFormParams) /* epsilon */;
							
FormParamList		::=		(MultipleFormParams) FormParamList COMMA FormParam
							|
							(SingleFormParam) FormParam;

FormParam			::=		(FormalParameterBrackets) Type IDENT:name LBRACKET RBRACKET
							|
							(FormalParameterNoBrackets) Type IDENT:name
							|
							error;
						
StatementList		::=		(Stmts) StatementList Statement
							|
							(NoStmt) /* epsilon */;
							
Statement			::=		(SingleStmt) SingleStatement
							|
							(MultipleStmts) Statements;

Statements			::=		(Statements) LBRACE StatementList RBRACE;

SingleStatement		::=		(IfStmt) IfStmtStart ThenStmtStart Statement
							|
							(IfElseStmt) IfStmtStart ThenStmtStart Statement ElseStmtStart Statement
							|
							(DoWhileStmt) DoStatementStart Statement WhileConditionStart LPAREN Condition RPAREN SEMI
							|
							(DesignatorStmt) DesignatorStatement SEMI
							|
							(BreakStmt) BREAK SEMI
							|
							(ContinueStmt) CONTINUE SEMI
							|
							(ReturnNoExprStmt) RETURN SEMI
							|
							(ReturnExprStmt) RETURN Expr:e SEMI
							|
							(ReadStmt) READ LPAREN Designator:d RPAREN SEMI
							|
							(PrintStmt) PRINT LPAREN PrintPars:p RPAREN SEMI;
									
IfStmtStart			::=		(IfStmtStart) IF;

ThenStmtStart		::=		(ThenStmtStart) LPAREN Condition RPAREN;

ElseStmtStart		::=		(ElseStmtStart) ELSE;
									
DoStatementStart	::=		(DoStatementStart) DO;

WhileConditionStart	::=		(WhileConditionStart) WHILE;

Expr				::=		(MultipleExpr) Expr:e Addop Term:t
							|
							(SingleExprWithMinus) MINUS Term:t
							|
							(SingleExpr) Term:t;
							
Term				::=		(MultipleTerm) Term:t Mulop Factor:f
							|
							(SingleTerm) Factor:f;

Factor				::=		(FactorWithConst) Const:c
							|
							(FactorWithParen) LPAREN Expr:e RPAREN
							|
							(FactorWithNew) NEW Type:t
							|
							(FactorWithNewArray) NEW Type:t LBRACKET Expr:e RBRACKET
							|
							(FactorDesignator) Designator:d
							|
							(FactorDesignatorFuncCall) FuncCallStart:f LPAREN OptActPars RPAREN;
							
FuncCallStart		::=		(FuncCallStart) Designator:d;
							
Designator			::=		(Designator) DesignatorName IndexingList;

DesignatorName		::=		(DesignatorName) IDENT:name;

IndexingList		::=		(MultipleIndexing) IndexingList Indexing
							|
							(NoIndexing) /* epsilon */;
							
Indexing			::=		(IndexingField) DOT IDENT:identName
							|
							(IndexingArray) LBRACKET Expr:e RBRACKET;
							
OptActPars			::=		(WithOptActPars) ActPars
							|
							(NoOptActPars) /* epsilon */; 
							
ActPars				::=		(MultipleActPars) ActPars COMMA Expr:e
							|
							(SingleActPars) Expr:e;
							
Mulop				::=		(Mul) MUL
							|
							(Div) DIV
							|
							(Percent) PERCENT;
							
Addop				::=		(Add) PLUS
							|
							(Minus) MINUS;
							
Assignop			::=		(Assignop) EQUAL;
					
PrintPars			::=		(PrintParameters) Expr:e
							|
							(PrintParametersWithConst) Expr:e COMMA NUMBER;
							
DesignatorStatement	::=		(DesignatorStmtAssign) Designator Assignop Expr
							|
							Designator Assignop error
							|
							(DesignatorStmtFuncCall) FuncCallStart:f LPAREN OptActPars RPAREN
							|
							(DesignatorStmtInc) Designator:d PLUSPLUS
							|
							(DesignatorStmtDec) Designator:d MINUSMINUS;
							
Condition			::=		(MultipleConditions) Condition:c OR CondTerm:ct
							|
							(SingleCondition) CondTerm:ct
							|
							error;
							
CondTerm			::=		(MultipleCondTerms) CondTerm:ct AND CondFact:cf
							|
							(SingleCondTerm) CondFact:ct;
							
CondFact			::=		(SingleCondFact) Expr
							|
							(EQCondFact) Expr:e1 EQ Expr:e2
							|
							(NEQCondFact) Expr:e1 NEQ Expr:e2
							|
							(GTCondFact) Expr:e1 GT Expr:e2
							|
							(GTECondFact) Expr:e1 GTE Expr:e2
							|
							(LTCondFact) Expr:e1 LT Expr:e2
							|
							(LTECondFact) Expr:e1 LTE Expr:e2;